plugins {
    id 'io.franzbecker.gradle-lombok' version '1.11'
    id 'java'
    id 'jacoco'
}

group theGroup
version theVersion
sourceCompatibility = theSourceCompatibility

apply from: 'gradle/script/nebula.gradle'
apply from: 'gradle/script/csa.gradle'
apply from: 'gradle/script/publishing.gradle'
apply from: 'gradle/script/report.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.hamcrest:hamcrest-core:1.3'
    compile 'org.hamcrest:hamcrest-library:1.3'
    compile 'org.testng:testng:6.10'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile("com.google.guava:guava:21.0") { force = true }
    compile 'io.cucumber:gherkin:5.0.0'
    compile 'io.cucumber:cucumber-jvm:2.3.1'
    compile 'io.cucumber:cucumber-core:2.3.1'
    compile 'io.cucumber:cucumber-testng:2.3.1'
    compile 'io.cucumber:cucumber-java:2.3.1'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.awaitility:awaitility:3.0.0'
    compile 'com.google.inject:guice:4.1.0'
    compileClasspath 'org.projectlombok:lombok:1.16.18'
    compileClasspath 'com.google.code.findbugs:findbugs:3.0.1'

    testCompile 'io.qameta.allure:allure-cucumber2-jvm:2.0-BETA21'
    testCompile 'ch.qos.logback:logback-classic:1.1.8'
    testCompile 'ch.qos.logback:logback-core:1.1.8'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompileClasspath 'org.projectlombok:lombok:1.16.18'
    testCompileClasspath 'com.google.code.findbugs:findbugs:3.0.1'
}

jar.dependsOn ':check'

check.dependsOn ':pmdMain', ':findbugsMain', ':checkstyleMain', ':jacocoTestCoverageVerification', ':jacocoTestReport'

test {
    systemProperties System.getProperties()
    useTestNG() {
        setUseDefaultListeners(true)
        setParallel("classes")
        includeGroups("unit")
        // TODO Restriction for tests that change RouterConfig constants
        setThreadCount(1)
    }
}

task demoViaFeatures(type: Test) {
    systemProperties System.getProperties()
    systemProperties.put("router.threadsPerEnv", 2)
    useTestNG() {
        setUseDefaultListeners(false)
        setParallel("instances")
        includeGroups("demoViaFeatures")
        setThreadCount(3)
    }
}

task demoViaScenarios(type: Test) {
    systemProperties System.getProperties()
    systemProperties.put("router.threadsPerEnv", 2)
    useTestNG() {
        setUseDefaultListeners(false)
        setParallel("instances")
        includeGroups("demoViaScenarios")
        setThreadCount(4)
    }
}

jacoco {
    toolVersion = "0.8.0"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}