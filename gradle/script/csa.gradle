enum CsaMode {
    DISABLED, XML, HTML
}

CsaMode csaMode = CsaMode.valueOf(System.getProperty("gradle.csa", "html").toUpperCase())

if (csaMode == CsaMode.DISABLED) return

apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

checkstyle {
    toolVersion = "8.4"
}

tasks.withType(Checkstyle) {
    ignoreFailures false
    setMaxErrors(0)
    setMaxWarnings(0)
    source = fileTree('src/main')
    config = resources.text.fromFile('config/checkstyle/config.xml')
    reports {
        xml.enabled csaMode == CsaMode.XML
        html.enabled csaMode == CsaMode.HTML
    }
}

checkstyleTest {
    ignoreFailures true
    source = fileTree('src/test')
}

tasks.withType(FindBugs) {
    classes = fileTree('build/classes/java/main')
    source = fileTree('src/main')
    reports {
        xml.enabled csaMode == CsaMode.XML
        html.enabled csaMode == CsaMode.HTML
    }
}

findbugsTest {
    classes = fileTree('build/classes/java/test')
    source = fileTree('src/test')
}

tasks.withType(Pmd) {
    source = fileTree('src/main')
    include '**/*.java'
    reports {
        xml.enabled csaMode == CsaMode.XML
        html.enabled csaMode == CsaMode.HTML
    }
}

pmdTest {
    source = fileTree('src/test')
}